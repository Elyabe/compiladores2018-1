
options {
	
  DEBUG_PARSER = false;
  DEBUG_TOKEN_MANAGER = false;

}

PARSER_BEGIN(Compilador)
import java.io.*;
import minhasClasses.*;
import java.util.LinkedList;

public class Compilador {
	static Tabela tabela = new Tabela();
	public static int erro = 0;
	public static void main(String args[])  throws ParseException  {
		tabela.marcador = 1;
    	Compilador analisador = null;
      	try {
	         analisador = new Compilador(new FileInputStream("exemplo17.spc2"));
	         Compilador.inicio();
	         if(erro == 1) 
	         System.out.println("Erro semantico\n"); 
			 else {  
				System.out.println("\nAnalise lexica, sintatica e semantica sem erros!");
	          	System.out.println("\nTabela de Simbolos:");
	 		 	System.out.println(tabela);
	 		 	System.out.println("\nMarcador:\n" + tabela.marcador);
	       	}
       	 }
	     catch(FileNotFoundException e) {
	         System.out.println("Erro: arquivo nao encontrado");
	     }
	     catch(TokenMgrError e) {
	        System.out.println("Erro lexico\n" + e.getMessage());
	     }
	     catch(ParseException e) {
	         System.out.println("Erro sintatico\n" + e.getMessage());
	     }
   }
}

PARSER_END(Compilador)

SKIP : { " " | "\t" | "\r" | "\n" }

//TOKENS DE COMANDOS 
TOKEN [IGNORE_CASE] : {
  		<EXIBE: "exibe" >  |
  		<LEITURA: "le" > | 
		<SE: "se" > |
      	<FIMSE: "fimse" > |
      	<ENQUANTO: "enquanto" > |
      	<FIMENQUANTO: "fimenquanto" > 
}
//TOKENS DE TIPOS 
TOKEN [IGNORE_CASE] : {
  		<NUMERO: "numero" >  |
  		<PALAVRA: "palavra" >  
}
//----------CARACTERES----------
TOKEN : {
		< #CHAR: ["a"-"z"] | ["A" - "Z"] > | 
   		< AP: "(" > |
    	< FP: ")" > |
		< PV: ";" > |
		< VIRGULA: "," > |
    	< STRING: "\"" (~["\""])* "\"" > |
   		< VAR: (< CHAR >)+ (["a" - "z", "A" - "Z" ,"0" - "9", "-"])* > 
}
//-----------NUMEROS-----------
TOKEN : {
	< #DIGIT: ["0"-"9"] > |  
    < NUM: (< DIGIT >)* (".")? (< DIGIT >)+ > 
}
//----------OPERADORES---------
TOKEN : {
 	< SOMA: "+" > |
	< SUB: "-" > |
	< MUL: "*" > |
    < DIV: "/" > |
    < POT: "^" > |
    < OU: "OU" > |
    < RECEBE: "<-"  > |
    < IGUAL: "=" > |
   	< CONCAT: "&" > 
}

void inicio() : {}	
	{
	      programa()  < EOF >
	}  
	void programa() : {}	
	{
	      (comando())*
	}
	void comando() : {} 
	{
	      atribui() | declaracao() | se() | enquanto() | le() | exibe()  
	}
	void atribui() : {Token t;} 
	{
		t = < VAR > {
			if (!tabela.isExiste(t.image)) {
					System.out.println("Variavel " + "'" + t.image + "'" + " nao foi declarada!");
					erro = 1;
			}
		}  <RECEBE> expressaoPrincipal() < PV >
	}
	void declaracao() : {Simbolo simb; Token t; char tp;}
	{
		(< NUMERO > {tp = 'n';} | < PALAVRA > { tp = 'p' ;}) t = < VAR > {
			if (tabela.isExiste(t.image)) {
				System.out.println("Variavel " + "'" + t.image + "'" + " ja declarada!");
				erro = 1;
      		}
			else if(tp == 'n') {
				  simb = new Simbolo(t.image, Tipo.numero, tabela.marcador);
				  tabela.inclui(simb);
				  tabela.marcador += 2;
			}
      		else {
				  simb = new Simbolo(Tipo.palavra, t.image, tabela.marcador);
				  tabela.inclui(simb);
				  tabela.marcador += 1;
		 	}
	  }
		(< RECEBE > expressaoPrincipal())? (< VIRGULA > t = < VAR > {
			if (tabela.isExiste(t.image)) {
				System.out.println("Variavel " +"'"+ t.image +"'" + " ja declarada!");
				erro = 1;
      		}
			else if(tp == 'n') {
				  simb = new Simbolo(t.image, Tipo.numero, tabela.marcador);
				  tabela.inclui(simb);
				  tabela.marcador += 2;
      		}
      		else {
				  simb = new Simbolo(Tipo.palavra, t.image, tabela.marcador);
				  tabela.inclui(simb);
				  tabela.marcador += 1;
      		}
	   	} (< RECEBE > expressaoPrincipal() )? )* < PV >
	}
	void se() : {}
	{
	      < SE > < AP > expressaoPrincipal() < FP > programa() < FIMSE >
	}
	void enquanto() : {}
	{
	      < ENQUANTO > < AP > expressaoPrincipal() < FP > programa() < FIMENQUANTO > 
	}
	void le() : {}
	{
	      <LEITURA> <VAR> ( < VIRGULA > < VAR > )* < PV > 
	}
	void exibe() : {}
	{
	      <EXIBE> expressaoPrincipal() ( < VIRGULA > expressaoPrincipal() )*   < PV > 
	}

	//==========GRAM�TICA DE EXPRESS�ES==============
	void expressaoPrincipal() : {LinkedList<Item> listaExp = new LinkedList<Item>();}  
	{
	     expressao(listaExp)	{ System.out.println(listaExp); }
	}
	void expressao(LinkedList<Item> listaExp) : {Token t; Item item = null;}  
	{
	      termo(listaExp) ( (t = < OU >) termo(listaExp){ listaExp.add(new Item(Tipo.operacao,t.image)); } )*  
	}
	void termo(LinkedList<Item> listaExp) : {Token t; Item item = null;}  
	{
	      termo1(listaExp) ( (t = < IGUAL >) termo1(listaExp)
	        	{
					listaExp.add(new Item(Tipo.operacao,t.image));
	          	}
	         )*
	         
	}
	void termo1(LinkedList<Item> listaExp) : {Token t; Item item = null;}  
	{
	     termo2(listaExp) ((t = < CONCAT >) termo2(listaExp) { listaExp.add(new Item(Tipo.operacao,t.image));} )*
	         
	}
    void termo2(LinkedList<Item> listaExp) : {Token t; Item item = null;}  
	{
	  		termo3(listaExp) ( (t = < SOMA > | t = < SUB > ) termo3(listaExp) {
	  			listaExp.add(new Item(Tipo.operacao,t.image));
	  		}
	        )*
	}
	void termo3(LinkedList<Item> listaExp) : {Token t; Item item = null;}  
	{
	      termo4(listaExp) ( (t = < MUL > | t = < DIV >) termo4(listaExp) {
				listaExp.add(new Item(Tipo.operacao,t.image));}
	       )*
	}
	void termo4(LinkedList<Item> listaExp) : {Token t; Item item = null;} 
	{
			< AP > expressao(listaExp) < FP >
		 	
		 	|t = < VAR > { 
		  		if (!tabela.isExiste(t.image)) {
					System.out.println("Erro semantico \n Variavel " +"'"+ t.image +"'" + " nao foi declarada!");
					erro = 1;
     			}
     			listaExp.add(new Item(Tipo.variavel,t.image));
         	}
		  	|t = < STRING > {
     			listaExp.add(new Item(Tipo.palavra,t.image));
		  	}
		  	
		  	|(< SOMA > | < SUB >)?  t =  <NUM> {
         		listaExp.add(new Item(Tipo.numero,t.image));
      		}	
	}