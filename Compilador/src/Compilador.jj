options {
  DEBUG_PARSER = false;
  DEBUG_TOKEN_MANAGER = false;
}

PARSER_BEGIN(Compilador)
import java.io.*;
import files.*;
import java.util.LinkedList;
public class Compilador {

   public static Tabela tab = new Tabela();
  
   public static void main(String args[])  throws ParseException  {
      Compilador compilador = null;
            
      try {
       	 
         compilador = new Compilador(new FileInputStream("exemplo20.spc"));
         Compilador.inicio();
         System.out.println("");
         tab.imprimeTabela();
      }
      catch(FileNotFoundException e) {
         System.out.println("Erro: arquivo nao encontrado");
      }
      catch(TokenMgrError e) {
         System.out.println("Erro lexico\n" + e.getMessage());
      }
   }
}
PARSER_END(Compilador)
 
SKIP : { " " | "\t" | "\r" | "\n" }

//TOKENS DE COMANDOS 
TOKEN [IGNORE_CASE] : {
  		<EXIBE: "exibe" >  |
  		<LEITURA: "le" > | 
		<SE: "se" > |
      	<FIMSE: "fim-se" > |
      	<ENQUANTO: "enquanto" > |
      	<FIMENQUANTO: "fim-enquanto" > 
}
//TOKENS DE TIPOS 
TOKEN [IGNORE_CASE] : {
  		<NUMERO: "numero" >  |
  		<PALAVRA: "palavra" >  
}
//----------CARACTERES----------
TOKEN : {
		< #CHAR: ["a"-"z"] | ["A" - "Z"] > | 
   		< AP: "(" > |
    	< FP: ")" > |
		< PV: ";" > |
		< VIRGULA: "," > |
    	< STRING: "\"" (~["\""])* "\"" > |
   		< VAR: (< CHAR >)+ (["a" - "z", "A" - "Z" ,"0" - "9", "_"])* > 
}
//-----------NUMEROS-----------
TOKEN : {
	< #DIGIT: ["0"-"9"] > |  
    < NUM: (< DIGIT >)* (".")? (< DIGIT >)+ > 
}
//----------OPERADORES---------
TOKEN : {
 	< SOMA: "+" > |
	< SUB: "-" > |
	< MUL: "*" > |
    < DIV: "/" > |
    < OU: "OR" > |
    < ATRIB: "<-"  > |
    < IGUAL: "=" > |
   	< CONCAT: "&" > 
}
  

//Gramatica de expressoes:

void inicioGramatica():
{LinkedList<Item> listaExp = new LinkedList<Item>();}
{
	expressao(listaExp)
	{System.out.println(listaExp);}
}

void expressao(LinkedList<Item> listaExp):
{Item item = new Item(); Token t;
}
{
	 termo(listaExp)(t=< OU > termo(listaExp)
	  {
	 	item.setTipo("operador");
	 	item.setValor(t.image);
	 	listaExp.add(item);
	 	
	 }
	 )*
		
}

void  termo(LinkedList<Item> listaExp):
{Item item = new Item(); Token t;}
{
  	termo1(listaExp) (t=< IGUAL > termo1(listaExp)
  	{
  		item.setTipo("operador");
	 	item.setValor(t.image);
	 	listaExp.add(item);
  	}
  	)?
}

void termo1(LinkedList<Item> listaExp):
{Item item = new Item(); Token t;}
{
  	termo2(listaExp) (t=< CONCAT > termo2(listaExp)
  	{
  		item.setTipo("operador");
	 	item.setValor(t.image);
	 	listaExp.add(item);
  	}
  	)*
} 
void termo2(LinkedList<Item> listaExp):
{Item item = new Item(); Token t;}
{
  	termo3(listaExp) ((t=< SOMA >|t=< SUB >) termo3(listaExp)
  	{
  		item.setTipo("operador");
	 	item.setValor(t.image);
	 	listaExp.add(item);
  	}
  	)*
}

void termo3(LinkedList<Item> listaExp):
{Item item = new Item(); Token t;}
{
   termo4(listaExp) ((t=< MUL >|t=< DIV >) termo4(listaExp)
   {
  		item.setTipo("operador");
	 	item.setValor(t.image);
	 	listaExp.add(item);
  	}
  	)*
}

void termo4(LinkedList<Item> listaExp):
{
	Item item = new Item(); 
	Token var, entrada, sinal;
}
{
 <AP> expressao(listaExp) <FP> 
|entrada=<NUM>
{
  	item.setValor(entrada.image);
  	item.setTipo("numerico");
  	listaExp.add(item);
} 
|<SOMA>entrada=<NUM>
{
  	item.setValor(entrada.image);
  	item.setTipo("numerico");
  	listaExp.add(item);
}
|sinal=<SUB>entrada=<NUM>
{
  	item.setValor(sinal.image+entrada.image);
  	item.setTipo("numerico");
  	listaExp.add(item);
}
|var=<VAR>
{
  	if(tab.verificaSimbolo(var.image) == false) {
  	  	System.out.println("Variavel nao Declarada");
  	  	return;
  	}
  	item.setValor(var.image);
  	item.setTipo((tab.pesquisaTabela(var.image)).getTipo()); 
  	listaExp.add(item);
}
|entrada=<STRING>
{
  	item.setValor(entrada.image);
  	item.setTipo("palavra");
  	listaExp.add(item);
}

}
 
 
//GRAMATICA COMPLETA

void inicio():
{}
{
	programa() <EOF>
}

void programa() :
{}
{
  (comando())* 
  

}

void comando():
{}
{
    atribuicao() | declaracao() | se() | enquanto() | le() | exibe()
}

void atribuicao():
{}
{
  	< VAR > < ATRIB > inicioGramatica() < PV >
}

void declaracao():
{ 	Simbolo simbolo;
 	Token tipo, variavel;
}
{
  	(tipo=<NUMERO>|tipo=<PALAVRA>) variavel=<VAR> 
  	{
  		if(tab.verificaSimbolo(variavel.image) == false) { 
	  	  simbolo = new Simbolo();
	  	  simbolo.setTipo(tipo.image);
	  	  simbolo.setNome(variavel.image);
	  	  simbolo.setReferencia(tab.getMarcador());
	  	  tab.incrementaMarcador(tipo.image);
	  	  tab.insereSimbolo(simbolo);
  	  }else {
  	    	System.out.println("Variavel "+ variavel.image + " Duplicada");
  	    	return;
  	  }
  	}
  	( <ATRIB> inicioGramatica() )? (<VIRGULA>  variavel=<VAR> ( <ATRIB> inicioGramatica() )? 
  	
  	 {
	  if(tab.verificaSimbolo(variavel.image) == false) { 
	  	  simbolo = new Simbolo();
	  	  simbolo.setTipo(tipo.image);
	  	  simbolo.setNome(variavel.image);
	  	  simbolo.setReferencia(tab.getMarcador());
	  	  tab.incrementaMarcador(tipo.image);
	  	  tab.insereSimbolo(simbolo);
  	  }else {
  	    	System.out.println("Variavel "+ variavel.image + " Duplicada");
  	    	return;
  	  }

  	}
  	
  	
  	)* <PV>
  	
  	
}

void se():
{}
{
  	<SE> <AP> inicioGramatica() <FP> programa() <FIMSE>
}
 
void enquanto():
{}
{
  	<ENQUANTO> <AP> inicioGramatica() <FP> programa() <FIMENQUANTO>
}
 
void le():
{}
{
  	<LEITURA> <VAR> ( <VIRGULA> <VAR> )* <PV>
}
 
void exibe():
{}
{
	<EXIBE> inicioGramatica() ( <VIRGULA> inicioGramatica() )* <PV>
}
	