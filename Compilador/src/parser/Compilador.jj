/* Compilador para a Linguagem Removename (extensao spc2)
   Turma de Compiladores 2018 - Eng de Computacao e Ciencia da Computacao
   UFES - Campus de Sao Mateus

   Git: https://github.com/hmcristovao/compiladores2018
   
	 Para executar (prompt no diretorio 'saida'):
	 > java -jar jasmin.jar teste.j
	 > java teste 

Obs.: o comando repetir-ate parou de funcionar (funcionava ate o commit da tarefa 14)
 
*/

options {
	DEBUG_PARSER = false;
	DEBUG_TOKEN_MANAGER = false;
}

PARSER_BEGIN(Compilador)

package parser;

import jasmin.*; 
import java.io.*;
import semantico.*;
import apoio.*;
import tratamentoErro.*;
import comandoAltoNivel.*;
import comandoPrimitivo.*;
import codigoDestino.*;
import java.util.LinkedList;


public class Compilador {
	public static Tabela tabelaPrograma = new Tabela( Config.nomeArquivo );
	// Argumentos para o jasmin gerar automaticamente o .class na pasta saida
	public static String arquivo[] = { "-d", Config.pathSaida, Config.pathSaida + Config.nomeArquivo + Config.extensaoCodigoDestino };
	public static String imageMain = Compilador.tokenImage[Compilador.MAIN];
	
	public static void main(String args[])  throws ParseException  {
		Compilador compilador = null;
		imageMain = imageMain.substring(1, imageMain.length()-1);
		
		try {
			// Leitura do arquivo com o codigo fonte
			compilador = new Compilador(new FileInputStream(Config.pathProgFonte + Config.nomeArquivo + Config.extensaoFonte));
			
			// Primeira passagem
			ListaComandosAltoNivel listaComandosAltoNivel = new ListaComandosAltoNivel();
			Compilador.inicio(listaComandosAltoNivel);
		
			System.out.println("***** Primeira Passagem *****\n"
								+ "Lista de Comandos Alto Nivel:\n"
								+ listaComandosAltoNivel);

			// Exibicao da tabela de simbolos
			System.out.println("\n\n***** Tabela de Simbolos *****\n"
								+ tabelaPrograma );

			//Warnings
			AcoesSemanticas.warnings();

			// Segunda passagem
			ListaComandosPrimitivos listaComandosPrimitivos = new ListaComandosPrimitivos();
			listaComandosPrimitivos = listaComandosAltoNivel.geraListaComandoPrimitivosCompleta();
			System.out.println("\n\n***** Segunda Passagem *****\n"
								+ "Lista de Comandos Primitivos:\n"
								+ listaComandosPrimitivos);

			// Terceira passagem
			CodigoDestino codigoDestino = new CodigoDestino(listaComandosPrimitivos);
			System.out.println("\n\n***** Terceira Passagem *****\n"
								+ "Lista de Comandos Destino:\n"
								+ codigoDestino);

			// Criacao do arquivo destino
			codigoDestino.geraArquivo(Config.pathSaida + Config.nomeArquivo + Config.extensaoCodigoDestino );
			System.out.println("\n\n***** Criacao do arquivo destino *****\n"
								+ "Arquivo destino: "
								+ Config.pathSaida + Config.nomeArquivo + Config.extensaoCodigoDestino);

			// Fim
			if(AcoesSemanticas.qtdWarnings > 0 ) {
			  System.out.println("\n\n***** Compilacao bem sucedida com "+AcoesSemanticas.qtdWarnings+" warnings! *****");
			}else {
				System.out.println("\n\n***** Compilacao bem sucedida! *****");
			}

			// Geracao automatizada do .class na pasta saida
			Main.main(arquivo);
			
		}
		
		catch(FileNotFoundException e) {
			System.err.println("\nErro: arquivo nao encontrado");
		}
		catch (IOException e) {
			System.out.println("Erro de criacao de arquivo" + e.getMessage());
		}
		catch(TokenMgrError e) {
			System.err.println("\nErro lexico: " + e.getMessage());
		}
		catch(ParseException e) {
			System.err.println("\nErro Sintatico: " + e.getMessage());
		}
		catch(ErroSemantico e) {
			System.err.println("\nErro Semantico: " + e.getMessage());
		}
	}
}
PARSER_END(Compilador)
 
SKIP : { " " | "\t" | "\r" | "\n" }

SKIP : { "/*" : comentarioGeral }
<comentarioGeral> SKIP: { "*/" : DEFAULT | <~[]> }

//TOKENS DE COMANDOS 
TOKEN [IGNORE_CASE] : {
	<EXIBE: "exibe" >  |
	<LEITURA: "le" > |
	<LEITURACOMPLETA: "lecompleto" > | 
	<SE: "se" > |
	<SENAO: "senao" > |
	<FIMSE: "fim-se" > |
	<ENQUANTO: "enquanto" > |
	<FIMENQUANTO: "fim-enquanto" > |
	<PARA: "para" > |
	<FIMPARA: "fim-para" > |
	<SELETOR: "seletor" > |
	<FIMSELETOR: "fim-seletor"> |
	<CASO: "caso"> |
	<FIMCASO: "fim-caso"> |
	<FACA: "faca"> |
	<FAZERENQUANTO: "fazer-enquanto"> |
	<REPEAT: "repeat" > |
	<FIMREPEAT: "fim-repeat"> |
	<SEREPETE: "se-repete"> |
	<FIMSEREPETE: "fim-se-repete"> |
	<DUPLO: "duplo"> |
	<FIMDUPLO: "fim-duplo"> |
	<MATRIX3D : "matrix3D" > |
	<FIMMATRIX3D: "fim-matrix3D" > |
	<MAX : "max" > |
	<MIN : "min" > |
	<FUNCAO: "funcao" > |
	<FIMFUNCAO: "fim-funcao" > |
	<RETORNE: "retorne" > |
	<PROCEDIMENTO: "procedimento" > |
	<FIMPROCEDIMENTO: "fim-procedimento" > |
	<MAIN: "cirocracia" > |
	<COMENTLINHA : "//" (~["\n"])* ("\n")? >   
}
//TOKENS DE TIPOS 
TOKEN [IGNORE_CASE] : {
	<NUMERO: "numero" >  |
	<PALAVRA: "palavra" >  
}

//-----------NUMEROS-----------
TOKEN : {
	< #DIGIT: ["0"-"9"] > |  
	< NUM: (< DIGIT >)* (".")? (< DIGIT >)+ > 
}
//----------OPERADORES---------
TOKEN : {
	< SOMA: "+" > |
	< SUB: "-" > |
	< MUL: "*" > |
	< DIV: "/" > |
	< POT : "**" >|
	< LOG: "log" > |
	< PISO: "piso" > |
	< TETO: "teto" > |
	< OU: "OU" > |
	< OUEXCLUSIVO: "XOU" > |
	< NEGACAO : "NAO" > |
	< E: "E" > |
	< ATRIB: "<-"  > |
	< ATRIBMULT: "<=="  > |
	< IGUAL: "=" > |
	< DIFERENTE: "!=" > |
	< CONCAT: "&" > |
	< MENOR: "<" > |
	< MAIORIGUAL: ">=" > |
	< TO: "to" > |
	< EXIBELN : "exibeln" >|
	< TERNARIO : "?" >
}
//----------CARACTERES----------
TOKEN : {
	< #CHAR: ["a"-"z"] | ["A" - "Z"] > | 
	< AP: "(" > |
	< FP: ")" > |
	< AC: "{" > |
	< FC: "}" > |
	< PV: ";" > |
	< DP: ":" > |
	< VIRGULA: "," > |
	< STRING: "\"" (~["\""])* "\"" >      	
}
//----------VARIAVEL---------
TOKEN : {
	< VAR: (< CHAR >)+ (["a" - "z", "A" - "Z" ,"0" - "9", "_"])* > 
}  

//Gramatica de expressoes:

Expressao iniciaExpressao(Tabela tabela):
{
	Expressao exp = new Expressao( tabela.getNome() );
}
{
	expressao(exp, tabela)
	{
	    exp.otimizarPosFixaConst1();
		exp.otimizaPosfixa();
		exp.otimizarNegacao();
//        exp.otimizarOperacoesConstantes();
		return exp;
	}	
}

void expressao(Expressao exp, Tabela tabela):
{
	Token t ;
	Item item = null;
}
{
	termo(exp, tabela)
	( ( t = <OU> { item = new Operador(TipoOperador.OU,t); } |
	    t = < OUEXCLUSIVO > {  item = new Operador(TipoOperador.OUEXCLUSIVO,t); }
	   ) 
		{
			exp.addListaExpInfixa(item);
		}
		termo(exp, tabela)
		{
			exp.addListaExpPosFixa(item);	 	
		}
	)*	 	
}

void termo(Expressao exp, Tabela tabela):
{
	Token t;
	Item item = null;
}
{
	termo1(exp, tabela)
	(t = <E>
		{
			item = new Operador(TipoOperador.E,t);
			exp.addListaExpInfixa(item);
		}
		termo1(exp, tabela)
		{
			exp.addListaExpPosFixa(item);
		}
	)* 
}

void  termo1(Expressao exp, Tabela tabela):
{
	Token t;
	Item item = null;
	TipoOperador operador;
}
{
	negacao(exp, tabela)
	(	  
	    (
	         t = <IGUAL> { operador = TipoOperador.IGUAL; } 
		   | t = <DIFERENTE> { operador = TipoOperador.DIFERENTE; }	
		   | t = <MENOR> { operador = TipoOperador.MENOR; } 
		   | t = <MAIORIGUAL> {operador = TipoOperador.MAIORIGUAL; }
	    )
	    {
			item = new Operador(operador,t);
			exp.addListaExpInfixa(item);
		}
		negacao(exp, tabela)
		{
			item = new Operador(operador,t);
			exp.addListaExpPosFixa(item);
		}
	)?
}

void negacao( Expressao exp, Tabela tabela ) :
{
	Token t;
	Item item = null;
}
{
	termo2(exp, tabela) |
	(t = <NEGACAO>
		{
			item = new Operador(TipoOperador.NEGACAO,t);
			exp.addListaExpInfixa(item);
		}
		negacao(exp, tabela)
		{
			exp.addListaExpPosFixa(item);
		}
	) 
}


void termo2(Expressao exp, Tabela tabela):
{
	Token t;
	Item item = null;
}
{
	termo3(exp, tabela)
	( t = <CONCAT>
		{
			item = new Operador(TipoOperador.CONCAT,t);
			exp.addListaExpInfixa(item);
		}
		termo3(exp, tabela)
		{
			item = new Operador(TipoOperador.CONCAT,t);
			exp.addListaExpPosFixa(item);
		}
	)*
}

void termo3(Expressao exp, Tabela tabela):
{
	Token t;
	TipoOperador operador;
	Item item = null;
}
{
	termo4(exp, tabela)
	(
		(
		    t = <SOMA> {operador = TipoOperador.SOMA;} 
		  | t = <SUB> {operador = TipoOperador.SUB;}
		)
		{
			item = new Operador(operador,t);
			exp.addListaExpInfixa(item);
		}
		termo4(exp, tabela)
		{
			item = new Operador(operador,t);
			exp.addListaExpPosFixa(item);
		}
	)*
}

void termo4(Expressao exp, Tabela tabela):
{
	Token t;
	TipoOperador operador;
	Item item = null;
}
{
	pot(exp, tabela)
	(
		(
		   t = <MUL> {operador = TipoOperador.MUL;} 
		 | t = <DIV> {operador = TipoOperador.DIV;}
		)
		{
			item = new Operador(operador,t);
			exp.addListaExpInfixa(item);
		}
		pot(exp, tabela)
		{
			item = new Operador(operador,t);
			exp.addListaExpPosFixa(item);
		}
	)*
}

void pot(Expressao exp, Tabela tabela) :
{
	Token t;
	TipoOperador operador;
	Item item = null;}
{
	log(exp, tabela)
	(
	  t=<POT> { operador = TipoOperador.POT; }
	  {  
	        item = new Operador(operador, t);
            exp.addListaExpInfixa(item);
	  } 
	  pot(exp, tabela)
	  {
	    	item = new Operador(operador, t);
            exp.addListaExpPosFixa(item);
	  }  
	)?
}

void log(Expressao exp, Tabela tabela) :
{
	Token t;
	TipoOperador operador;
	Item item = null;
}
{
  	termo5(exp, tabela) |
	(
	  ( t = <LOG> { operador = TipoOperador.LOG; }  |
	  	t = <PISO> { operador = TipoOperador.PISO; } |
	  	t = <TETO> { operador = TipoOperador.TETO; }
	   ) 
	  	<AP> expressao(exp,tabela) <FP>  |
	  (
	    t = <MAX> { operador = TipoOperador.MAX; } |
	    t = <MIN> { operador = TipoOperador.MIN; }
	   ) <AP> expressao(exp, tabela) <VIRGULA> expressao(exp, tabela) <FP> 
	) 
  	{
        item = new Operador(operador, t);
        exp.addListaExpInfixa(item);
        exp.addListaExpPosFixa(item);
    }
}

void termo5(Expressao exp, Tabela tabela):
{
	Token tokenVar, tokenEntrada;
	Item item = null;
	Sinal sinal = Sinal.POS;
	LinkedList<Expressao> listaParam = new LinkedList<Expressao>();
	Expressao expParam;
}
{
	tokenEntrada = <AP>
	{
		item = new Operador(TipoOperador.AP, tokenEntrada);
		exp.addListaExpInfixa(item);
	}
	expressao(exp, tabela)
	tokenEntrada = <FP>
	{
		item = new Operador(TipoOperador.FP, tokenEntrada);
		exp.addListaExpInfixa(item);
	}
	|  ( <SUB > { sinal = Sinal.NEG; } | <SOMA> { sinal = Sinal.POS; } )?
		( tokenEntrada = <NUM>
			{
				item = new Operando(TipoDado.NUMERO,TipoElemento.CTE, tokenEntrada, sinal);
				AcoesSemanticas.incompatibilidadeTipoExpressao(item,exp,tokenEntrada);
				exp.addListaExpInfixa(item);
				exp.addListaExpPosFixa(item);
			}
			|  tokenVar = <VAR> { item = new Operando(tabela.tipoVariavel(tokenVar.image),TipoElemento.VAR, tokenVar, sinal); }
			   ( <AP> expParam = iniciaExpressao(tabela) { listaParam.add( expParam ); }
			   		(<VIRGULA> expParam = iniciaExpressao(tabela){ listaParam.add( expParam ); } )* <FP>
			   		{
			   		  item = new ChamadaFuncao( TipoOperador.FUNCAO, tokenVar, tokenVar, listaParam );
			   		  //tabela.pesquisarSimboloTabela(tokenVar.image).setIsUtilizada(true);
			   		}
			   )?				{
//					tabela.verificaVariavelDeclarada(tokenVar.image);
//					AcoesSemanticas.incompatibilidadeTipoExpressao(item,exp,tokenVar);
					exp.addListaExpInfixa(item);
					exp.addListaExpPosFixa(item);
				}
		)
	| tokenEntrada = <STRING>
	{
		item = new Operando(TipoDado.PALAVRA,TipoElemento.CTE, tokenEntrada, Sinal.POS);
		AcoesSemanticas.incompatibilidadeTipoExpressao(item,exp,tokenEntrada);
		exp.addListaExpInfixa(item);
		exp.addListaExpPosFixa(item);
	}
}
 
 
//GRAMATICA COMPLETA

void inicio(ListaComandosAltoNivel listaComandosAltoNivel):
{}
{	
	(
	  < COMENTLINHA > |
	  procedimento(listaComandosAltoNivel) |
	  funcao(listaComandosAltoNivel)
	 )*
	{ AcoesSemanticas.verificarExistenciaMetodoMain(); }
	<EOF>
}

void procedimento( ListaComandosAltoNivel listaComandosAltoNivel ):
{
  Token tokenProcedimento, tokenAssinatura, tokenVar;
  LinkedList<Token> listaParametros = new LinkedList<Token>();
  TipoDado tipoParam;
  ComandoAltoNivel comando;
  ListaComandosAltoNivel listaCorpoProcedimento = new ListaComandosAltoNivel();
}
{
	tokenProcedimento = <PROCEDIMENTO>
	(tokenAssinatura = <VAR> | tokenAssinatura = <MAIN>) { Compilador.tabelaPrograma.insereNamespaceNaTabela( tokenAssinatura, TipoOperador.PROCEDIMENTO ); }
	<AP>
	( ( <PALAVRA> { tipoParam =  TipoDado.PALAVRA; }  |  <NUMERO> { tipoParam =  TipoDado.NUMERO; } )
    	tokenVar = <VAR>
    	{
    	  Compilador.tabelaPrograma.pesquisarTabela( tokenAssinatura.image ).insereSimboloNaTabela( tokenVar, tipoParam);
    	  Compilador.tabelaPrograma.pesquisarTabela( tokenAssinatura.image ).pesquisarSimboloTabela( tokenVar.image ).setIsInicializada(true);
    	  listaParametros.add( tokenVar );
    	}
	  (
	       <VIRGULA> ( <PALAVRA> { tipoParam =  TipoDado.PALAVRA; } | <NUMERO> { tipoParam =  TipoDado.NUMERO; } )?
	      tokenVar = <VAR>
	      {
	        Compilador.tabelaPrograma.pesquisarTabela( tokenAssinatura.image ).insereSimboloNaTabela( tokenVar, tipoParam);
	        Compilador.tabelaPrograma.pesquisarTabela( tokenAssinatura.image ).pesquisarSimboloTabela( tokenVar.image ).setIsInicializada(true);
	        listaParametros.add( tokenVar );
	      }
	  )*
 	)?
 	<FP>
	programa(listaCorpoProcedimento, Compilador.tabelaPrograma.pesquisarTabela( tokenAssinatura.image ) ) { AcoesSemanticas.otimizarComandosCondicionais( listaCorpoProcedimento ); }
	<FIMPROCEDIMENTO>
	{
		comando = new Procedimento( tokenProcedimento, tokenAssinatura, listaParametros, listaCorpoProcedimento );
		listaComandosAltoNivel.addComando( comando );  
	}
}

void funcao( ListaComandosAltoNivel listaComandosAltoNivel ):
{
  Token tokenFuncao, tokenAssinatura, tokenVar;
  LinkedList<Token> listaParametros = new LinkedList<Token>();
  TipoDado tipoParam, tipoRetorno;
  ComandoAltoNivel comando;
  ListaComandosAltoNivel listaCorpoProcedimento = new ListaComandosAltoNivel();
}
{
	tokenFuncao = <FUNCAO>
	tokenAssinatura = <VAR> { Compilador.tabelaPrograma.insereNamespaceNaTabela( tokenAssinatura, TipoOperador.FUNCAO ); }
	<AP>
	( ( <PALAVRA> { tipoParam =  TipoDado.PALAVRA; }  |  <NUMERO> { tipoParam =  TipoDado.NUMERO; } )
    	tokenVar = <VAR>
    	{
    	  Compilador.tabelaPrograma.pesquisarTabela( tokenAssinatura.image ).insereSimboloNaTabela( tokenVar, tipoParam);
    	  Compilador.tabelaPrograma.pesquisarTabela( tokenAssinatura.image ).pesquisarSimboloTabela( tokenVar.image ).setIsInicializada(true);
    	  listaParametros.add( tokenVar );
    	}
	  (
	       <VIRGULA> ( <PALAVRA> { tipoParam =  TipoDado.PALAVRA; } | <NUMERO> { tipoParam =  TipoDado.NUMERO; } )?
	      tokenVar = <VAR>
	      {
	        Compilador.tabelaPrograma.pesquisarTabela( tokenAssinatura.image ).insereSimboloNaTabela( tokenVar, tipoParam);
	        Compilador.tabelaPrograma.pesquisarTabela( tokenAssinatura.image ).pesquisarSimboloTabela( tokenVar.image ).setIsInicializada(true);
	        listaParametros.add( tokenVar );
	      }
	  )*
 	)?
 	<FP> <DP> ( <NUMERO> { tipoRetorno = TipoDado.NUMERO; } | <PALAVRA> { tipoRetorno = TipoDado.PALAVRA; } )
 	{
		Compilador.tabelaPrograma.pesquisarTabela( tokenAssinatura.image ).setTipoRetorno( tipoRetorno );
 	}
	programa(listaCorpoProcedimento, Compilador.tabelaPrograma.pesquisarTabela( tokenAssinatura.image ) ) { AcoesSemanticas.otimizarComandosCondicionais( listaCorpoProcedimento ); }
	<FIMFUNCAO>
	{
		comando = new Funcao( tokenFuncao, tokenAssinatura, listaParametros, listaCorpoProcedimento, tipoRetorno );
		listaComandosAltoNivel.addComando( comando );  
	}
}

void retorne( ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela ):
{
  Token tokenRetorne;
  Expressao expressao;
  ComandoAltoNivel comandoRetorne;
}
{
	tokenRetorne = <RETORNE> expressao  = iniciaExpressao( tabela )
	{
		comandoRetorne = new ComandoRetorne( tokenRetorne, expressao );
		listaComandosAltoNivel.addComando( comandoRetorne );
	}
}


void programa(ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela) :
{}
{
	(comando(listaComandosAltoNivel, tabela))*
}

void comando(ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela):
{}
{
	(
		comandoInstanciacao(listaComandosAltoNivel, tabela)
//	  | chamadaProcedimentoFuncao(listaComandosAltoNivel)
	  | se(listaComandosAltoNivel, tabela)
	  | enquanto(listaComandosAltoNivel, tabela)
	  | le(listaComandosAltoNivel, tabela)
	  | exibe(listaComandosAltoNivel, tabela)
	  | para(listaComandosAltoNivel, tabela)
	  | seletor(listaComandosAltoNivel, tabela)
	  | fazerEnquanto(listaComandosAltoNivel, tabela)
	  | repeat(listaComandosAltoNivel, tabela)
	  | seRepete(listaComandosAltoNivel, tabela)
	  | duplo(listaComandosAltoNivel, tabela)
	  | matrix3D( listaComandosAltoNivel, tabela)
	  | ternario( listaComandosAltoNivel, tabela)
	  | retorne( listaComandosAltoNivel, tabela ) <PV>
	)	
}

void comandoInstanciacao( ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela ):
{}
{
	  atribuicaoChamada(listaComandosAltoNivel, tabela) <PV>
	| declaracao(listaComandosAltoNivel, tabela)
	| < COMENTLINHA > 
}

void atribuicaoChamada(ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela):
{
	Token atrib, var;
	LinkedList<Expressao > listaExp = new LinkedList<Expressao >();
}
{
	var = <VAR> ( 	atribuicao( listaComandosAltoNivel, var, tabela ) 
  					| chamadaProcedimentoFuncao( listaComandosAltoNivel, var, tabela ) 	
   				)
}

void chamadaProcedimentoFuncao( ListaComandosAltoNivel listaComandosAltoNivel, Token tokenAssinatura, Tabela tabela ):
{
  Token tokenVar;
  LinkedList<Expressao> listaParametros = new LinkedList<Expressao>();
  ComandoAltoNivel comando;
  Expressao expressaoParam;
}
{
	 <AP> ( expressaoParam = iniciaExpressao( tabela ) { listaParametros.add( expressaoParam ); }
				( <VIRGULA> expressaoParam = iniciaExpressao( tabela ) { listaParametros.add( expressaoParam ); } )*
		   )?
	 <FP>
	 {
		AcoesSemanticas.verificarExistenciaMetodo( tokenAssinatura );
		comando = new ChamadaMetodo( tokenAssinatura, listaParametros );
		listaComandosAltoNivel.addComando( comando );
	 }
}

void atribuicao(ListaComandosAltoNivel listaComandosAltoNivel, Token Var, Tabela tabela ):
{
	Token atrib, var = Var;
	TipoOperador tpOperador;
	Item operando, operador;
//	ComandoAltoNivel comando = null;
	Expressao expressao = null;
	LinkedList<Token > listaVar = new LinkedList<Token >();
	listaVar.add(Var);
	LinkedList<Expressao > listaExp = new LinkedList<Expressao >();
}
{
   (     (
	     atrib = <ATRIB> expressao = iniciaExpressao(tabela) { listaExp.add( expressao); } |
	
	     ( < VIRGULA > var = < VAR > {  tabela.verificaVariavelDeclarada(var.image); listaVar.add(var); } )* 
		 atrib = < ATRIBMULT >
		               expressao = iniciaExpressao(tabela) { listaExp.add( expressao ); }
		 ( < VIRGULA > expressao = iniciaExpressao(tabela) {  listaExp.add( expressao ); } )*
   	 )
	 {
		 AcoesSemanticas.verificarParesAtribuicaoMultipla( listaVar, listaExp, atrib );  
		 AcoesSemanticas.gerarAtribuicoesMultiplas( listaComandosAltoNivel, tabela, listaVar, listaExp, atrib );
	 } |
		
	 ( atrib =  <SOMA> { tpOperador = TipoOperador.SOMA; } |
	   atrib = <SUB> { tpOperador = TipoOperador.SUB; } |
	   atrib = <MUL> { tpOperador = TipoOperador.MUL; } |
	   atrib = <DIV> { tpOperador = TipoOperador.DIV; }
	 )
	 {
	   operador = new Operador( tpOperador, atrib );
       operando = new Operando( tabela.tipoVariavel(var.image), TipoElemento.VAR, var, Sinal.POS );
       expressao = new Expressao(tabela.getNome());
       expressao.addListaExpInfixa( operando );
       expressao.addListaExpInfixa( operador );
       expressao.addListaExpPosFixa( operando );
	 } 
     <IGUAL> expressao( expressao, tabela )
	 {
		expressao.addListaExpPosFixa( operador );
		listaComandosAltoNivel.addComando( new ComandoAtribuicao( tabela.pesquisarSimboloTabela(var.image), expressao, atrib ) );
	 }  	 
   )
}

void declaracao(ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela):
{   
	Token atrib, variavel;
	TipoDado tipo = null;
	ComandoAltoNivel comando = null;
	Expressao expressao = null;
}
{
	(
	    <NUMERO> {tipo = TipoDado.NUMERO;}
	  | <PALAVRA> {tipo = TipoDado.PALAVRA;}
	)
	
	variavel = <VAR> 
	{
		tabela.insereSimboloNaTabela(variavel, tipo);
	}
	( 
		atrib = <ATRIB> expressao=iniciaExpressao(tabela) 
		{
			AcoesSemanticas.incompatibilidadeTipoAtribuicao (tabela.pesquisarSimboloTabela(variavel.image), expressao, variavel);
			comando = new ComandoAtribuicao(tabela.pesquisarSimboloTabela(variavel.image), expressao, atrib);
			tabela.pesquisarSimboloTabela(variavel.image).setIsInicializada(true);			
			AcoesSemanticas.faltaInicializacaoVariavel(expressao,tabela, variavel);			
			listaComandosAltoNivel.addComando(comando);
		}
	)?
	(
		<VIRGULA> variavel = <VAR>
		{
			tabela.insereSimboloNaTabela(variavel, tipo);
		}
		(
			atrib = <ATRIB> expressao=iniciaExpressao(tabela)
			{
				AcoesSemanticas.incompatibilidadeTipoAtribuicao (tabela.pesquisarSimboloTabela(variavel.image), expressao, variavel);
				comando = new ComandoAtribuicao(tabela.pesquisarSimboloTabela(variavel.image), expressao, atrib);
				tabela.pesquisarSimboloTabela(variavel.image).setIsInicializada(true);
				AcoesSemanticas.faltaInicializacaoVariavel(expressao,tabela,variavel);
				listaComandosAltoNivel.addComando(comando);
			}
		)?
	)* <PV>
}

void  se(ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela):
{
	Token se,senao;
	ListaComandosAltoNivel listaProgramaSe =  new ListaComandosAltoNivel();	
	ListaComandosAltoNivel listaProgramaSenao =  new ListaComandosAltoNivel();
	Expressao expressao = null;
	ComandoAltoNivel  comando = null;
}
{
	se = <SE> <AP> expressao = iniciaExpressao(tabela) <FP> programa(listaProgramaSe, tabela)
	{
		comando = new ComandoCondicionalSimples(expressao, listaProgramaSe, se);
		AcoesSemanticas.faltaInicializacaoVariavel(expressao,tabela,se);
	}  
	(
	  senao = <SENAO> programa(listaProgramaSenao, tabela)
	  {
	    comando = new ComandoCondicionalCompleto(expressao, listaProgramaSe, listaProgramaSenao, se);
	  }
	)?
	<FIMSE>
	{
	  listaComandosAltoNivel.addComando(comando); 
	}	
}

void  fazerEnquanto(ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela):
{
	Token fazer;
	ListaComandosAltoNivel listaProgramaFazerEnquanto = new ListaComandosAltoNivel();
	Expressao expressao = null;
	ComandoAltoNivel comando = null;
}
{
	fazer = <FACA>programa(listaProgramaFazerEnquanto, tabela)<FAZERENQUANTO><AP>expressao = iniciaExpressao(tabela)<FP>< PV>
	{
		comando = new ComandoFazerEnquanto(expressao, listaProgramaFazerEnquanto, fazer);
		AcoesSemanticas.faltaInicializacaoVariavel(expressao,tabela, fazer);
		listaComandosAltoNivel.addComando(comando);
	}
}

void  enquanto(ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela):
{
	Token enq;
	ListaComandosAltoNivel listaProgramaEnquanto = new ListaComandosAltoNivel();
	Expressao expressao = null;
	ComandoAltoNivel comando = null;
}
{
	enq = <ENQUANTO> <AP> expressao = iniciaExpressao(tabela) <FP> programa(listaProgramaEnquanto, tabela)
	{
		comando = new ComandoEnquanto(expressao, listaProgramaEnquanto, enq);
		AcoesSemanticas.faltaInicializacaoVariavel(expressao,tabela,enq);
		listaComandosAltoNivel.addComando(comando);  
	}
	<FIMENQUANTO>
}
 
void le(ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela):
{
	Token le, t;
	ComandoAltoNivel comando = null;
}
{
	  (   le = <LEITURA> |
		  le = <LEITURACOMPLETA> t = <STRING> <VIRGULA> 
		  {
			Expressao expressao = new Expressao(tabela.getNome());
			Item string = new Operando( TipoDado.PALAVRA, TipoElemento.CTE, t, Sinal.POS );
			expressao.addListaExpInfixa( string );
			expressao.addListaExpPosFixa( string );
		    comando  = new ComandoSaida( expressao, le );
		    listaComandosAltoNivel.addComando( comando );
		  }
	  )
	t = <VAR>
	{
		comando = new ComandoEntrada(tabela.pesquisarSimboloTabela(t.image), le);
		tabela.pesquisarSimboloTabela(t.image).setIsInicializada(true);
		tabela.pesquisarSimboloTabela(t.image).setIsUtilizada(true);
		listaComandosAltoNivel.addComando(comando);  
	}
	( <VIRGULA> t = <VAR>
		{
			comando = new ComandoEntrada(tabela.pesquisarSimboloTabela(t.image), le);
			tabela.pesquisarSimboloTabela(t.image).setIsInicializada(true);
			listaComandosAltoNivel.addComando(comando);		
		}
	)* <PV>
}
 
void exibe(ListaComandosAltoNivel listaExibe, Tabela tabela):
{
	Token exibe;
	boolean novaLinha = false;
	Expressao expressao = null;
	ComandoSaida comando = null;
}
{
	( exibe = <EXIBE> | exibe = <EXIBELN> { novaLinha = true; })  expressao = iniciaExpressao(tabela)
	{
		comando = new ComandoSaida(expressao, exibe);
		AcoesSemanticas.faltaInicializacaoVariavel(expressao,tabela,exibe);
		listaExibe.addComando(comando); 
	}
	( <VIRGULA> expressao = iniciaExpressao(tabela)
		{
			comando = new ComandoSaida(expressao, exibe);
			AcoesSemanticas.faltaInicializacaoVariavel(expressao,tabela,exibe);
			listaExibe.addComando(comando);   
		}
	)* <PV>
	{
		if ( novaLinha ) AcoesSemanticas.adicionarQuebraLinha( listaExibe, tabela.getNome(), exibe );
	}
}

void para(ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela):
{
  	Token tokenPara;
  	ListaComandosAltoNivel listaProgramaPara = new ListaComandosAltoNivel();
  	Expressao expressao = null;
  	ComandoAltoNivel comando = null;
  	ListaComandosAltoNivel listaAtribuicaoInicio = new ListaComandosAltoNivel();
  	ListaComandosAltoNivel listaAtribuicaoPasso = new ListaComandosAltoNivel();
}
{
  	tokenPara = <PARA> <AP> atribuicaoChamada(listaAtribuicaoInicio, tabela) <PV>
  										 expressao=iniciaExpressao( tabela ) <PV>
  										 atribuicaoChamada(listaAtribuicaoPasso, tabela)
  					   <FP> programa(listaProgramaPara, tabela)
  			   <FIMPARA>
  	{
  	  	comando = new ComandoPara(listaAtribuicaoInicio, expressao, listaAtribuicaoPasso, listaProgramaPara, tokenPara);
  	  	AcoesSemanticas.faltaInicializacaoVariavel(expressao,tabela, tokenPara);
  	  	listaComandosAltoNivel.addComando(comando);
 	}
}

void seletor(ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela):
{
  	Token tokenSeletor, tokenEntrada;
  	Item itemCaso = null;
  	Expressao expressaoSeletor = null;
  	ComandoAltoNivel comando = null;
  	ListaComandosAltoNivel listaPrograma = new ListaComandosAltoNivel();
  	LinkedList<ListaComandosAltoNivel> listaComandosAltoNivelCasos = new LinkedList<ListaComandosAltoNivel>();
  	LinkedList<Item> listaOpcaoCasos = new LinkedList<Item>(); 
  	
}
{
 	tokenSeletor = <SELETOR> <AP> expressaoSeletor = iniciaExpressao(tabela) <FP>
 	(<CASO>
 	(
 	   	tokenEntrada = <NUM> {itemCaso = new Operando(TipoDado.NUMERO,TipoElemento.CTE, tokenEntrada, Sinal.POS);}
		|<SOMA> tokenEntrada = <NUM> {itemCaso = new Operando(TipoDado.NUMERO,TipoElemento.CTE, tokenEntrada, Sinal.POS);}
		|<SUB> tokenEntrada = <NUM> {itemCaso = new Operando(TipoDado.NUMERO,TipoElemento.CTE, tokenEntrada, Sinal.NEG);}
	)
 	<DP> programa(listaPrograma, tabela)
 	{
 	  listaComandosAltoNivelCasos.add(listaPrograma);
 	  listaOpcaoCasos.add(itemCaso);
 	  listaPrograma = new ListaComandosAltoNivel();
 	  
 	}
 	<FIMCASO>)+
 	{
 	  comando = new ComandoSeletor(expressaoSeletor, listaComandosAltoNivelCasos, listaOpcaoCasos, token);
 	  AcoesSemanticas.faltaInicializacaoVariavel(expressaoSeletor,tabela, tokenSeletor);
  	  listaComandosAltoNivel.addComando(comando);
 	}
 	<FIMSELETOR> 	 
}

void repeat(ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela):
{
  	Token tokenRepeat, tokenNumero;
  	ListaComandosAltoNivel listaProgramaRepeat = new ListaComandosAltoNivel();
  	ComandoAltoNivel comando = null;
  	Item item;
}
{
  tokenRepeat = <REPEAT>
  <AP> tokenNumero = <NUM>
  {
      item = new Operando(TipoDado.NUMERO,TipoElemento.CTE, tokenNumero, Sinal.POS);
  }
  <FP> programa(listaProgramaRepeat, tabela)< FIMREPEAT>
  {
      comando = new ComandoRepeat(listaProgramaRepeat, tokenRepeat, item);
  	  listaComandosAltoNivel.addComando(comando);
  }
}


void  seRepete(ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela):
{
	Token se_repete, token_repete_n;
	Expressao exp = null;
	Item item;
	ListaComandosAltoNivel lista_escopo_SeRepete = new ListaComandosAltoNivel();	
	ComandoAltoNivel comando = null;
}
{  	
	se_repete = <SEREPETE> <AP> exp = iniciaExpressao(tabela) <VIRGULA>  token_repete_n = <NUM>
	{
		item = new Operando(TipoDado.NUMERO,TipoElemento.CTE, token_repete_n, Sinal.POS);
	} <FP> programa(lista_escopo_SeRepete, tabela)
	{	  	
		comando = new ComandoSeRepete(exp, token_repete_n, item, lista_escopo_SeRepete);
		AcoesSemanticas.faltaInicializacaoVariavel(exp, tabela, se_repete);
		listaComandosAltoNivel.addComando(comando);  
	}
	<FIMSEREPETE>
}

void duplo(ListaComandosAltoNivel listaComandosAltoNivel, Tabela tabela):
{
	Token tokenDuplo;
	ListaComandosAltoNivel listaProgramaDuplo = new ListaComandosAltoNivel();
	ComandoAltoNivel comando = null;
}
{
  	tokenDuplo = <DUPLO> programa(listaProgramaDuplo, tabela) 
  	{
    	comando = new ComandoDuplo(listaProgramaDuplo, tokenDuplo);
		listaComandosAltoNivel.addComando(comando);
  	}
  	<FIMDUPLO>
}

// Tarefa 18
void matrix3D(ListaComandosAltoNivel listaMatrix3D, Tabela tabela ):
{
  	Token tokenMatrix3D, tokenVarDimX, tokenVarDimY, tokenVarDimZ, tokenCteInit, tokenCteLimSup;
  	ComandoAltoNivel comandoMatrix3D = null;
  	Item cteInicializacao = null;
	Item cteLimiteSuperior = null;
	Expressao expInicializacao = new Expressao(tabela.getNome());
	Expressao expCondicaoParada = new Expressao(tabela.getNome());
  	ListaComandosAltoNivel blocoComandosMatrix = new ListaComandosAltoNivel();
}
{
	tokenMatrix3D = <MATRIX3D> <AP>
				tokenVarDimX = <VAR>
				{
				  if ( !tabela.verificarNamespace( tokenVarDimX.image ) ) tabela.insereSimboloNaTabela( tokenVarDimX, TipoDado.NUMERO );
				  tabela.pesquisarSimboloTabela( tokenVarDimX.image ).setIsInicializada(true);
				} <VIRGULA>
				tokenVarDimY = <VAR>
				{
				  if ( !tabela.verificarNamespace( tokenVarDimY.image ) ) tabela.insereSimboloNaTabela( tokenVarDimY, TipoDado.NUMERO );
				  tabela.pesquisarSimboloTabela( tokenVarDimY.image ).setIsInicializada(true);
				} <VIRGULA>
				tokenVarDimZ = <VAR>
				{
				  if ( !tabela.verificarNamespace( tokenVarDimZ.image ) ) tabela.insereSimboloNaTabela( tokenVarDimZ, TipoDado.NUMERO );
				  tabela.pesquisarSimboloTabela( tokenVarDimZ.image ).setIsInicializada(true);
				} <SUB>
				tokenCteInit = <NUM>
				{
				  cteInicializacao = new Operando( TipoDado.NUMERO, TipoElemento.CTE, tokenCteInit, Sinal.POS );
				  expInicializacao.addListaExpInfixa( cteInicializacao );
				  expInicializacao.addListaExpPosFixa( cteInicializacao );
				}  <TO>
				tokenCteLimSup = <NUM>
				{
				  cteLimiteSuperior = new Operando( TipoDado.NUMERO, TipoElemento.CTE, tokenCteLimSup, Sinal.POS );
				  expCondicaoParada.addListaExpInfixa( cteLimiteSuperior );
				  expCondicaoParada.addListaExpPosFixa( cteLimiteSuperior );
				} <FP> programa( blocoComandosMatrix, tabela ) <FIMMATRIX3D>
				{
					comandoMatrix3D = new ComandoMatrix3D( tabela.getNome(), tokenMatrix3D, tokenVarDimX, tokenVarDimY, tokenVarDimZ, expInicializacao, expCondicaoParada, blocoComandosMatrix );
					listaMatrix3D.addComando( comandoMatrix3D );
				}
}

void ternario( ListaComandosAltoNivel listaTernario , Tabela tabela):
{
	Token tokenTernario;
	Expressao expressao;
	ListaComandosAltoNivel listaTernarioTrue = new ListaComandosAltoNivel();
	ListaComandosAltoNivel listaTernarioFalse = new ListaComandosAltoNivel();
	ComandoAltoNivel comandoCondicionalCompleto;
}
{
  	<AP> expressao = iniciaExpressao(tabela) <FP> tokenTernario = <TERNARIO>
  	<AC> programa(listaTernarioTrue, tabela) <FC> <DP>
  	<AC> programa(listaTernarioFalse, tabela) <FC>
  	{
		comandoCondicionalCompleto = new ComandoCondicionalCompleto( expressao, listaTernarioTrue, listaTernarioFalse, tokenTernario );
		listaTernario.addComando( comandoCondicionalCompleto );
  	}   
}