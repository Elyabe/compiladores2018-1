options {
  DEBUG_PARSER = false;
  DEBUG_TOKEN_MANAGER = false;
}

PARSER_BEGIN(Compilador)
package parser;
import semantico.*;
import apoio.*;
import java.io.*; 
import java.util.LinkedList;
import geradorCodigo.*;
import comandos.*;
import primitivo.*;

public class Compilador {

	public static Tabela tabela = new Tabela();
	static ListaComandosAltoNivel listaCAN;
	static ListaComandosPrimitivos listaCP;
	
   public static void main(String args[])  throws ErroSemantico
   {
      Compilador compilador = null;
      
      try {        
        compilador = new Compilador(new FileInputStream("exemplosSPC/exemplo08.spc"));

		listaCAN = new ListaComandosAltoNivel();
		listaCP = new ListaComandosPrimitivos();
        listaCAN = Compilador.one_line();

		listaCP = listaCAN.geraListaComandosPrimitivosTotal();

		//System.out.println(listaCAN);
   		System.out.println(listaCP);
      	//System.out.println(tabela);
      }
      catch(FileNotFoundException e) {
         System.out.println("Erro: arquivo nao encontrado");
      }
      catch(ErroSemantico e) {
         System.out.println("Erro semantico\n" + e.getMessage());
      }
      catch(TokenMgrError e) {
         System.out.println("Erro lexico\n" + e.getMessage());
      }
      catch(ParseException e) {
         System.out.println("Erro sintático\n" + e.getMessage());
      }
      
   }
}

PARSER_END(Compilador)

SKIP : { " " | "\t" | "\r" | "\n" }

//TOKENS DE COMANDOS
TOKEN [IGNORE_CASE] : {
  		<EXIBE: "exibe" >  |
  		<LEITURA: "le" > | 
		<SE: "se" > |
      	<FIMSE: "fim-se" > |
      	<ENQUANTO: "enquanto" > |
      	<FIMENQUANTO: "fim-enquanto" > 
}
//TOKENS DE TIPOS 
TOKEN [IGNORE_CASE] : {
  		<NUMERO: "numero" >  |
  		<PALAVRA: "palavra" >  
}
//----------CARACTERES----------
TOKEN : {
		< #CHAR: ["a"-"z"] | ["A" - "Z"] > | 
   		< AP: "(" > |
    	< FP: ")" > |
		< PV: ";" > |
		< VIRGULA: "," > |
    	< STRING: "\"" (~["\""])* "\"" > |
   		< VAR: (< CHAR >)+ (["a" - "z", "A" - "Z" ,"0" - "9", "_"])* > 
}
//-----------NUMEROS-----------
TOKEN : {
	< #DIGIT: ["0"-"9"] > |  
    < NUM: (< DIGIT >)* (".")? (< DIGIT >)+ > 
}
//----------OPERADORES---------
TOKEN : {
 	< SOMA: "+" > |
	< SUB: "-" > |
	< MUL: "*" > |
    < DIV: "/" > |
    < OU: "OR" > |
    < ATRIB: "<-"  > |
    < IGUAL: "=" > |
   	< CONCAT: "&" > 
}

//--------------------------------------------------------------------------------------------

Expressao expressao():{Expressao e = new Expressao();}
{
	iniciaExp(e)
	{
	 	//System.out.println(e.listaExpPosfixa);
	
	  return e;
	}
}

void iniciaExp(Expressao e):{Token s; Item item = null;}
{
	termo(e)	(s = < OU > termo(e)
	{
		//e.listaExpPosfixa.add(new Item(Tipo.operacao,s.image));
		
	  item = new Operador(TipoOperador.OR,s);	  
	  e.listaExpPosfixa.add(item);
	}
	)*
}

void termo(Expressao e):{Token s; Item item = null;}
{
	termo1(e) (s = < IGUAL > termo1(e)
	{
	  	//e.listaExpPosfixa.add(new Item(Tipo.operacao,s.image));
	  item = new Operador(TipoOperador.COMP,s);	  
	  e.listaExpPosfixa.add(item);
	
	}
	)?
}

void termo1(Expressao e):{Token s; Item item = null;}
{
	termo2(e) (s = < CONCAT > termo2(e)
	{
		//e.listaExpPosfixa.add(new Item(Tipo.operacao,s.image));
		item = new Operador(TipoOperador.CONCAT,s);	  
	  	e.listaExpPosfixa.add(item);
	
	}
	)*
}

void termo2(Expressao e): {Token s; Item item = null; TipoOperador tipo = null;}
{
	termo3(e)
	((s = < SOMA > {tipo = TipoOperador.ADD; }
	| s = < SUB >{tipo = TipoOperador.SUB; }) termo3(e)	  
	{
	  //e.listaExpPosfixa.add(new Item(Tipo.operacao, s.image));
	  
	  item = new Operador(tipo,s);	  
	  e.listaExpPosfixa.add(item);
	  
	}	
	)*
}

void termo3(Expressao e): {Token s; Item item = null; TipoOperador tipo = null;} {
	termo4(e)
	((s = < MUL > {tipo = TipoOperador.MUL; }
	| s = < DIV >{tipo = TipoOperador.DIV; }) termo4(e)
	{
	  //e.listaExpPosfixa.add(new Item(Tipo.operacao, s.image));

	  item = new Operador(tipo,s);	  
	  e.listaExpPosfixa.add(item);
	}	
	)*
}

void termo4(Expressao e): {Token s,a; Item item = null;}{
	< AP > expressao() <FP >
	| a = < NUM >
	{
	  //e.listaExpPosfixa.add(new Item(Tipo.numero, a.image));
	  item = new Operando(TipoDado.NUM,TipoElemento.CTE,a);
	  e.listaExpPosfixa.add(item);

	}
	
	| s = < SOMA >	a = <NUM >
	{
	  //e.listaExpPosfixa.add(new Item(Tipo.numero, a.image));
	  //e.listaExpPosfixa.add(new Item(Tipo.operacao, s.image));
	  item = new Operando(TipoDado.NUM,TipoElemento.CTE,a);
	  e.listaExpPosfixa.add(item);
	  
	  item = new Operador(TipoOperador.ADD,s);	  
	  e.listaExpPosfixa.add(item);

	}
	
	| s = < SUB >	a = <NUM >
	{
	  //e.listaExpPosfixa.add(new Item(Tipo.numero, a.image));
	  //e.listaExpPosfixa.add(new Item(Tipo.operacao, s.image));
	  item = new Operando(TipoDado.NUM,TipoElemento.CTE,a);
	  e.listaExpPosfixa.add(item);
	  
	  item = new Operador(TipoOperador.SUB,s);
	  e.listaExpPosfixa.add(item);

	}
	
	| s = < VAR >
	{	  
	  if(tabela.tab.get(s.image).getTipo().equals(TipoDado.STR)) {
			item = new Operando(TipoDado.STR, TipoElemento.VAR, s);
	  		e.listaExpPosfixa.add(item);
	  }
	  else {
			item = new Operando(TipoDado.NUM, TipoElemento.VAR, s);
	  		e.listaExpPosfixa.add(item);
	  }
	  

	}
	 
	| s = < STRING >
	{
	  item = new Operando(TipoDado.STR, TipoElemento.CTE, s);
	  e.listaExpPosfixa.add(item);
	}
}

//----------------------------------FUNCOES INICIAIS--------------------------------------------
ListaComandosAltoNivel one_line() : {ListaComandosAltoNivel listaComandos;}
{
   listaComandos = inicio() <EOF>
   {return listaComandos;}
}

ListaComandosAltoNivel inicio():
{ComandoAltoNivel cmd = null; ListaComandosAltoNivel listaComandos = new ListaComandosAltoNivel(); } {
	(comando(listaComandos))*
	{
	  return listaComandos;
	}< EOF>
}

void comando(ListaComandosAltoNivel listaCAN): {} {
  ( atribuicao(listaCAN)
  | declaracao(listaCAN)
  | se(listaCAN)
  | enquanto(listaCAN)
  | le(listaCAN)
  | exibe(listaCAN)
  )  
}

void atribuicao(ListaComandosAltoNivel listaCAN): {Expressao e; Token var,cmd; Simbolo simb = null; ComandoAltoNivel comando;}{
	var = < VAR >
	{
  		AcoesSemanticas.inicializacao(tabela, var.image);
		simb = new Simbolo(var.image,TipoDado.STR,tabela.marcador);
  		
	}
	cmd = < ATRIB > e = expressao()
	{
	  comando = new ComandoAtribuicao(simb,e,cmd);
	  listaCAN.addComando(comando);
	}
	< PV >
}

void declaracao(ListaComandosAltoNivel listaCAN):{Expressao e; Simbolo simb = null; Token var,cmd; TipoDado tipo; ComandoAltoNivel comando;} {
  	
	(< NUMERO >{ tipo = TipoDado.NUM; } | < PALAVRA >{ tipo = TipoDado.STR; })
	var=< VAR >
	{
	  	AcoesSemanticas.declaracao(tabela, var.image, simb, tipo);
	  	comando = null;
	}
	(cmd = < ATRIB > e = expressao()
	{	  
		simb = new Simbolo(var.image,tipo,tabela.marcador);
		comando = new ComandoAtribuicao(simb,e,cmd);
		
	  	listaCAN.addComando(comando);
	}
	)?
	(< VIRGULA > var = < VAR >(cmd = < ATRIB > e = expressao()
	{
		simb = new Simbolo(var.image,tipo,tabela.marcador);
		comando = new ComandoAtribuicao(simb,e,cmd);

		listaCAN.addComando(comando);
	})?
	{
	  	AcoesSemanticas.declaracao(tabela, var.image, simb, tipo);
	}
	)*
	< PV >
}

void se(ListaComandosAltoNivel listaCAN):{Expressao e; Token var,cmd; ComandoAltoNivel comando; ListaComandosAltoNivel lista = null;} {
	cmd = < SE > <AP > e = expressao() < FP > lista = inicio()
	{
		comando = new ComandoCondicional(e,lista,cmd);

		listaCAN.addComando(comando);
	} < FIMSE >
}

void enquanto(ListaComandosAltoNivel listaCAN):{Expressao e; Token var,cmd; ComandoAltoNivel comando; ListaComandosAltoNivel lista = null;} {

	cmd = < ENQUANTO > < AP > e = expressao() < FP > lista = inicio()
	{
		comando = new ComandoEnquanto(e,lista,cmd);

		listaCAN.addComando(comando);
	}
	< FIMENQUANTO >
}

void le(ListaComandosAltoNivel listaCAN):
{Simbolo simb = null; Token cmd, r; ComandoAltoNivel comando; ComandoEntrada cmdE;} {
	cmd = < LEITURA > r = < VAR >
	{
		simb = new Simbolo(r.image, TipoDado.STR,tabela.marcador);
		comando = new ComandoEntrada(simb ,cmd);

		listaCAN.addComando(comando);
	}
	(< VIRGULA > r = <VAR >
	{
	  simb = new Simbolo(r.image, TipoDado.STR,tabela.marcador);
	  comando = new ComandoEntrada(simb ,cmd);

	  listaCAN.addComando(comando);	
	}
	)*< PV >
}

void exibe(ListaComandosAltoNivel listaCAN):{Expressao e; ComandoAltoNivel comando; Token cmd; } {
	cmd = < EXIBE > e = expressao()
	{
	  comando = new ComandoSaida(e,cmd);

	  listaCAN.addComando(comando);
	}
	(< VIRGULA > e = expressao()
	{
	  comando = new ComandoSaida(e,cmd);

	  listaCAN.addComando(comando);
	}
	)*< PV >
}